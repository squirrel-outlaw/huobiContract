def versionPropsFile = file("version.properties")
Properties versionProps = new Properties()

ext {
    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
        Alihost = versionProps['AliyunHostIP'].toString()
        Aliuser = versionProps['AliyunHostUser'].toString()
        Alipwd = versionProps['AliyunHostPwd'].toString()
    } else {
        throw new GradleException("Could not read version.properties!")
    }
}

buildscript {
    repositories {
        maven { url = 'http://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }
    dependencies {
        classpath('org.hidetake:gradle-ssh-plugin:2.9.0')
        classpath('com.github.jengelman.gradle.plugins:shadow:1.2.4')
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'org.hidetake.ssh'

group = 'com.huobi.api'
version = '1.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    maven { url = 'http://maven.aliyun.com/nexus/content/groups/public/' }
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.7'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.24'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.24'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3'
    compile group: 'org.bouncycastle', name: 'bcprov-ext-jdk15on', version: '1.59'
    compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.0'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.6'
    compile group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.3.0'
    compile group: 'com.squareup.retrofit2', name: 'converter-jackson', version: '2.3.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    // 处理excel文档的依赖
    compile group: 'net.sourceforge.jexcelapi', name: 'jxl', version: '2.6.10'
    compile group: 'org.apache.poi', name: 'poi', version: '3.10.1'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.10.1'
    compile group: 'org.apache.poi', name: 'poi-ooxml-schemas', version: '3.10.1'
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'com.huobi.api.Main'
    }
    //打包配置文件
    from('src') {
        include '**/*.txt', '*.properties'
    }

}

ssh.settings {
    knownHosts = allowAnyHosts
}

remotes {
    deployServer {
        host = Alihost
        user = Aliuser
        password = Alipwd
    }
}


task del() {
    doLast {
        ssh.run {
            session(remotes.deployServer) {
                println 'start deleting...'
                executeScript '''#!/bin/sh  
                        cd /usr/local/huobi
                        rm -rf RE* 
                    '''
            }
        }
    }
}

task copyJar(dependsOn: del) {
    dependsOn shadowJar
    doLast {
        ssh.run {
            session(remotes.deployServer) {
                println 'start copying jar...'
                put from: shadowJar.archivePath, into: '/usr/local/huobi'
            }
        }
    }
}

task copyFileFromAliyun() {
    doLast {
        ssh.run {
            session(remotes.deployServer) {
                println 'start copying file...'
                get from: '/usr/local/huobi/AliyunData', into: buildDir
            }
        }
    }
}

task tempcopyFileToAliyun() {
    doLast {
        ssh.run {
            session(remotes.deployServer) {
                println 'start copying file...'
                put from: "C:\\Documents and Settings\\Administrator\\桌面\\AliyunData", into:
                        '/usr/local/huobi'
            }
        }
    }
}
